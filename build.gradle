buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

setupScala()

apply plugin: 'forge'

minecraft {
    version = "1.7.10-10.13.0.1180"
    assetDir = "eclipse/assets"
}
	
version = "1.2.0-1.7.10"
archivesBaseName = "randomredstone"
group = "com.theenginerd.randomredstone"

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

def setupScala() {
    apply plugin: 'scala'

    repositories {
        maven {
            name = "scala-tools"
            url = "https://oss.sonatype.org/content/repositories/scala-tools/"
        }
    }

    configurations.create("scalaCompilerPlugins")
    configurations.scalaCompilerPlugins.transitive = false

    dependencies {
        scalaCompilerPlugins 'org.scalamacros:paradise_2.11.1:2.1.0-M1'
    }

    compileScala {
        scalaCompileOptions.additionalParameters = (["-feature"] + (configurations.scalaCompilerPlugins.files.collect { "\"-Xplugin:${it.path}\"" }))*.toString()
    }
}
